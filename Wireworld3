# -----------------------------------------------------------------------------
# WireWorld : Wireworld cellular automata
#             https://en.wikipedia.org/wiki/Wireworld
# -
# Description :
# - WireWorld is based on 3-state mineral agents: 'conductor', 'head', 'tail'
# - 'conductor' simulates path for moving electrons
# - 'head' and 'tail' are the two adjacent parts of a moving electron
# - 'head' generates an 'electric' field only captured by 'conductor'
# -----------------------------------------------------------------------------
world 7 6 #FFF

mineral conductor #FF0
var active                   # default: InitValue=0 TimeStepValue=0
status active < 3 head       # 'conductor' -> 'head' only when 'active' == 1 or 2
status active < 1 conductor
status active > 2 conductor
sensor active electric 1

mineral tail #F00
status conductor             # 'tail' -> 'conductor' without condition

mineral head #00F
var electric 2               # default: TimeStepValue=0
status tail                  # 'head' -> 'tail' without condition
field electric -1

# set two electrons moving on a single horizontal wire (use slice notation)
keys (h,head) (c,conductor) (t,tail)
. . c c . .
c c c . c c
c . c c . c
c . . . . h
c . c c . c
c c . c c c
. . c c . .
# -----------------------------------------------------------------------------